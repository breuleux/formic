
require:
    lib
    common
    highlight

cr = "\n"[0]

Source = [text, url = false] ->

    lines = V enumerate[chain[text, "\n"[0] :: nil]] each [i, c] ->
        if (c == cr):
            i
        else:
            (false, continue)

    self =
        [r] -> text[r]
        .text -> text
        .url -> url
        .nlines -> lines.length
        .linecol -> [pos] ->
            if (pos < 0 or pos > text.length):
                raise Error.source.outofbounds[
                    source = self
                    position = pos
                    length = text.length
                ]
            current = -1
            do enumerate[lines, 1] each [i, n] ->
                if (pos == n):
                    break [i + 1, 0]
                elif (pos < n):
                    break [i, pos - current]
                current := n
        r when (r == repr) ->
            #struct[repr[.Source], repr[url]]

        .highlight_lines -> [Range lrange, spans = nil, => raw = false] ->
            hl0 = highlight_lines[text, lines, lrange, spans]
            hl = #attr[#set["lines"], hl0]
            if[raw, hl, Repr[hl]]

    self


Location =
  context = dyn[0]

  .context -> context
  [source, Range [start, end]] ->

    self =
        .source -> source
        .start -> start
        .end -> end

        .range -> start..end
        .text -> source[start..end]

        .linecol ->
            incl_end = end - 1
            [l1, c1] = source.linecol[start]
            if (start > incl_end):
                [[l1, c1], false]
            else:
                [[l1, c1], source.linecol[incl_end]]

        .linerange ->
            source.linecol[start][0] .. (source.linecol[end][0] + 1)

        .ref ->
            match self.linecol:
                [[l1, c1], false _] ->
                    (++)[String[l1], ":", String[c1]]
                [[l1, c1], [l2, c2]] ->
                    if (l1 == l2 and c1 == c2):
                        (++)[String[l1], ":", String[c1]]
                    elif (l1 == l2):
                        (++)[String[l1], ":", String[c1], "-", String[c2]]
                    else:
                        (++)[String[l1], ":", String[c1], "-", String[l2], ":", String[c2]]

        .at_start -> Location[source, start .. start]
        .at_end -> Location[source, end .. end]

        .highlight -> [cls = "hl1", => context = 0] ->
            highlight_locations[self => cls, context = context]

        #dispatch[0, x] when (x == (+)) -> [loc2] ->
            merge_locations[self, loc2]

        #dispatch[0, x] when (x == (|)) -> [loc2] ->
            LocationComplex[self, loc2]

        x when (x == repr) ->
            #attr[#set["location"]
                  #group[#group[repr[source.url or Repr[#text["<unknown>"]]]]
                         #group[#text[self.ref]]]
                  highlight_locations[self => "hl1", context = @context, raw = true]]

    self


LocationComplex = [*_locations] ->

    locations = V
        chain _locations each loc ->
            loc.locations !! [loc]

    .locations -> locations

    #dispatch[0, x] when (x == (|)) -> [loc2] ->
        LocationComplex[*locations, loc2]

    x when (x == repr) ->
        highlights = A enumerate[locations] each [i, loc] ->
                         loc :: ("hl" ++ String[i + 1])
        source = locations[0].source
        #attr[#set["location"]
              #group[#group[repr[source.url or Repr[#text["<unknown>"]]]]
                     #cat(locations each loc -> #group[#text[loc.ref]])]
              _highlight_locations[highlights, @(Location.context), true]]


merge_locations = Vector locations ->
    if (locations.length == 0):
        Location[false, 0..0]
    else:
        start = min locations each loc -> loc.start
        end = max locations each loc -> loc.end
        Location[locations[0].source, start..end]


highlight_locations = [=> context = 0, => raw = false, **locations] ->
    _highlight_locations[locations, context, raw]

_highlight_locations = [locations, context, raw] ->
    loc = merge_locations(V locations each Pair [loc, cls] -> loc)
    source = loc.source
    Range [line1, line2] = loc.linerange
    first = max[1, line1 - context]
    last = min[source.nlines + 1, line2 + context]
    source.highlight_lines[
        first .. last
        locations each Pair [loc, cls] -> [loc.range, [cls]]
        raw = raw
    ]


provide:
    Source
    Location
    merge_locations

